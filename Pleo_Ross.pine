//@version=3
// study(title="Pleo_Ross Ichimoku Cloud", shorttitle="Pleo", overlay=false)
strategy(title="Pleo_Ross v3", shorttitle="PleoV3", overlay=false)

//Ichimoku input Logic
conversionPeriods = input(9, minval=1, title="Conversion Line Periods"),
basePeriods = input(26, minval=1, title="Base Line Periods")
laggingSpan2Periods = input(52, minval=1, title="Lagging Span 2 Periods"),
displacement = input(26, minval=1, title="Displacement")

//Ichimoku function Logic
donchian(len) => avg(lowest(len), highest(len))

//Ichimoku line Logic
tenkanLine = donchian(conversionPeriods)
kijunLine = donchian(basePeriods)
leadLine1 = avg(tenkanLine, kijunLine)
leadLine2 = donchian(laggingSpan2Periods)

// store state of open position (whether long or short)
lastOpenPosition = 0.0
lastOpenPosition := lastOpenPosition[1]

// previous values persistence
previousTenkan = 0.0
previousTenkan := nz(tenkanLine[1])
previousKijun = 0.0
previousKijun := nz(kijunLine[1])

// find where the lines cross
c = cross(tenkanLine, kijunLine)

//logic for seeing the trend
isBlueUp = previousTenkan < previousKijun
weakBuy = c and isBlueUp
weakSell = c and (not isBlueUp)
isAboveCloud = close > leadLine1 and close > leadLine2
isBelowCloud = close < leadLine1 and close < leadLine2
strongBuy = weakBuy and isAboveCloud
strongSell = weakSell and isBelowCloud

// plot visuals
// plot(tenkanLine, color=blue, linewidth=3, title="Tenkan Line")
// plot(kijunLine, color=red, linewidth=3, title="Kijun Line")
plot(close)//, offset = -displacement, color=green, linewidth=2, title="Chikou Lagging Span")
//plot(lastOpenPosition)
// p1 = plot(leadLine1, offset = displacement, color=white, title="Lead 1")
// p2 = plot(leadLine2, offset = displacement, color=white, title="Lead 2")
// fill(p1, p2, color = leadLine1 > leadLine2 ? green : red)

// Opening positions
plotshape(weakBuy and not strongBuy, style=shape.arrowup, location=location.belowbar, color=green, text="Weak\nBuy", size=size.small)
plotshape(strongBuy, style=shape.arrowup, location=location.belowbar, color=green, text="Strong\nBuy", size=size.large)
plotshape(weakSell and not strongSell, style=shape.arrowdown, location=location.abovebar, color=red, text="Weak\nSell", size=size.small)
plotshape(strongSell, style=shape.arrowdown, location=location.abovebar, color=red, text="Strong\nSell", size=size.large)

hasPreviousBuy = lastOpenPosition[1] > 0
hasPreviousSell = lastOpenPosition[1] < 0

if (weakBuy and not hasPreviousBuy)
    strategy.close("sell")
    if strongBuy
        lastOpenPosition := 2
        strategy.entry("buy", strategy.long, 2)
    else
        lastOpenPosition := 1
        strategy.entry("buy", strategy.long, 1)

if (weakSell and not hasPreviousSell)
    strategy.close("buy") 
    if strongSell    
        lastOpenPosition := -2
        strategy.entry("sell", strategy.short, 2)
    else
        lastOpenPosition := -1
        strategy.entry("sell", strategy.short, 1)
